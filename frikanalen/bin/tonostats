#!/usr/bin/perl

use strict;
use warnings;
use vars qw(%opts);

use Getopt::Std;

# SOAP:Lite må modifiseres til å gjøre ting på MS måten :-/
use SOAP::Lite on_action => sub {sprintf '%s/%s', @_}, ;

sub get_tono_stats {
    my $soap = new SOAP::Lite
        -> uri('http://localhost/CommunitySiteService')
        -> proxy('http://communitysite1.frikanalen.tv/CommunitySiteFacade/CommunitySiteService.asmx');

    my $obj = $soap->SearchVideos(
        SOAP::Data->name('searcher' => {
            'PredefinedSearchType' => 'Default',
            'Take' => 10000,
                         }
        )
        );
    if ($obj->fault) {
        print join ', ',
              $obj->faultcode,
              $obj->faultstring;
        return;
    }

    my $res = $obj->result;
#    print Dumper($res);
    unless ($res->{'Data'}) {
        return;
    }
    my $tono = 0;
    my $notono = 0;
    foreach my $video (@{$res->{'Data'}->{'Video'}}) {
        if ("true" eq $video->{'HasTonoRecords'}) {
            $tono++;
        } else {
            $notono++;
        }
    }
    return ($tono, $notono);
}
sub print_tono_stats {
    my ($with, $without) = get_tono_stats();
    print("Innslag med tono-musikk:   $with (",
          int( 100 * $with / ($with+$without)),"%)\n");
    print("Innslag uten tonon-musikk: $without (",
          int( 100 * $without / ($with+$without)),"%)\n");
}
sub munin_plugin {
    my @ARGV = @_;
    if (defined $ARGV[0] && "config" eq $ARGV[0]) {
        print <<EOF;
graph_title Fraction of events with Tono-related music
graph_category Frikanalen
tono_frac.label fraction
EOF
    } else {
        my ($with, $without) = get_tono_stats();
        printf "tono_frac.value %.3f\n", ($with / ($with + $without));
    }
}

getopts("m", \%opts);

if ($opts{'m'}) {
    munin_plugin(@ARGV);
} else {
    print_tono_stats();
}
